library TraceUtils;

modeltype TRACE uses trace('http:///www.eclipse.org/m2m/qvt/operational/trace.ecore');
modeltype QVTO uses qvtoperational('http://www.eclipse.org/QVT/1.0.0/Operational');
modeltype ECORE uses ecore('http://www.eclipse.org/emf/2002/Ecore');
modeltype NOTATION uses notation('http://www.eclipse.org/gmf/runtime/1.0.2/notation');

/*
=============================================
              Global Variables           
=============================================

Ensure that you initialize these properties before
calling mappings/constructors/helpers/queries in
the section named "property dependent".
*/

property TARGET_TRANSFORMATION_NAME : String;
property TARGET_TRANSFORMATION : qvtoperational::expressions::OperationalTransformation;


/*
=============================================
           Common Trace Mappings
============================================= 
*/

mapping notation::BasicCompartment::traceRecordExtended_basicCompartment() : List(trace::TraceRecord)
{
	result += new TraceRecord("titleStyle", "EObject", self.eobject(), "TitleStyle", self.styles->selectByKind(notation::TitleStyle)->any(true).eobject());
	result += new TraceRecord("sortingStyle", "EObject", self.eobject(), "SortingStyle", self.styles->selectByKind(notation::SortingStyle)->any(true).eobject());
	result += new TraceRecord("filteringStyle", "EObject", self.eobject(), "FilteringStyle", self.styles->selectByKind(notation::FilteringStyle)->any(true).eobject());
	result += self.map traceRecordExtended_notationBounds();
}

mapping notation::Node::traceRecordExtended_locationConstraint() : trace::TraceRecord
{
	init {
		result := new TraceRecord("locationConstraint", "EObject", self.eobject(), "Location", self.layoutConstraint.eobject());
	}
}

mapping notation::Node::traceRecordExtended_notationBounds() : trace::TraceRecord
{
	init {
		result := new TraceRecord("notationBounds", "EObject", self.eobject(), "Bounds", self.layoutConstraint.eobject());
	}
}

mapping notation::DecorationNode::traceRecordExtended_visibleTrueAnnotation() : List(trace::TraceRecord)
{
	var relevantAnnotation := self.eAnnotations->any(source = "PapyrusCSSForceValue");
	result += new TraceRecord("visibleTrueAnnotation", "DecorationNode", self.eobject(), "EAnnotation", relevantAnnotation.eobject());
	result += new TraceRecord("annotationToVisibleDetail", "EAnnotation", relevantAnnotation.eobject(), "EStringToStringMapEntry", relevantAnnotation.details->any(key = "visible").eobject());
}


/*
=============================================
              Property Dependent        
=============================================

These utilities require properly initialized
properties.
 
*/

/**
 * Creates a trace record from the target model's point of view.
 *
 * Assumption: Combination of mapping name and context type is unique.
 *  
 * mappingName The name of the mapping operation of the target model
 * contextType The type of the context (self) element
 * contextValue The context (self) element
 * resultType The type of the result element
 * resultValue The result element
 * 
 */
constructor trace::TraceRecord::TraceRecord(mappingName : String, contextType : String, contextValue : ecore::EObject, resultType : String, resultValue : ecore::EObject)
{
	mappingOperation := object trace::EMappingOperation {
		name := mappingName;
		package := mappingName.getMappingModuleName();
		module := mappingName.getMappingModuleName();
		//runtimeMappingOperation := TARGET_TRANSFORMATION.getMapping(mappingName, contextType);
	};
	context := object trace::EMappingContext {
		context := object trace::VarParameterValue {
			name := "self";
			type := contextType;
			value := new EValue(contextValue);
		};
	};
	parameters := object trace::EMappingParameters {};
	result._result := new EMappingResults(resultType, resultValue);			
}

constructor trace::TraceRecord::TraceRecord(mappingName : String, contextType : String, contextValue : ecore::EObject)
{
	mappingOperation := object trace::EMappingOperation {
		name := mappingName;
		package := mappingName.getMappingModuleName();
		module := mappingName.getMappingModuleName();
		//runtimeMappingOperation := TARGET_TRANSFORMATION.getMapping(mappingName, contextType);
	};
	context := object trace::EMappingContext {
		context := object trace::VarParameterValue {
			name := "self";
			type := contextType;
			value := new EValue(contextValue);
		};
	};
	parameters := object trace::EMappingParameters {};
		
}
/*
=============================================
              Regular Utilities        
=============================================

These utilities make no assumptions.
 
*/

query ecore::EObject::getRoot() : ecore::EObject
{
	var parent := self.eobject();
	while(parent.eContainer() != null) {
		parent := parent.eContainer();
	};
	assert error (parent != null) with log ('Could not find a parent.', self);
	return parent;
}

query trace::VarParameterValue::getTraceRecord() : trace::TraceRecord
{
	var parent := self.eobject();
	while(parent != null and not parent.oclIsKindOf(trace::TraceRecord)) {
		parent := parent.eContainer();
	};

	assert error (parent.oclIsKindOf(trace::TraceRecord)) with log('Could not find parent trace record.', self);

	return parent.oclAsType(trace::TraceRecord);
}

query trace::TraceRecord::getResult() : trace::EValue
{
	var resultMapping := self.getResultMapping();
	return resultMapping.value;
}

query trace::TraceRecord::getResultMapping() : trace::VarParameterValue
{
	var results := self._result._result->select((kind = EDirectionKind::OUT or kind = EDirectionKind::INOUT) and name = "result");
	assert error (results->size() = 1) with log('Records with multiple results are not supported.', self);
	return results->any(true);
}

query trace::TraceRecord::getSelf() : trace::EValue
{
	return self.getContextMapping().value;
}

query trace::TraceRecord::getContextMapping() : trace::VarParameterValue
{
	return self.context.context;
}

query OclAny::eobject() : ecore::EObject
{
	assert fatal (self.oclIsKindOf(ecore::EObject)) with log('Conversion to EObject only possible for EObjects.');
	return self.oclAsType(ecore::EObject);
}

query qvtoperational::expressions::OperationalTransformation::getMapping(name : String, contextType : String) : qvtoperational::expressions::MappingOperation {
	var operations := self.eAllOperations->selectByKind(qvtoperational::expressions::MappingOperation);
	var matchingOperations := operations->select(o | o.name = name and o.context.eType.name = contextType);
	assert error (matchingOperations->size() = 1) with log ('The mapping operation has to be identifyable unambigously.', name);
	return matchingOperations->any(true);
}


constructor trace::EValue::EValue(target : Integer)
{
	primitiveValue := target.toString();
	collectionType := "Integer"
}

constructor trace::EValue::EValue(target : ecore::EObject)
{
	modelElement := target;
}

constructor trace::EValue::EValue(objects : OrderedSet(OclAny))
{
	collectionType := "OrderedSet";
	objects->forEach(c) {
		collection += new EValue(c.eobject())
	}
}

constructor trace::VarParameterValue::VarParameterValue(parameterName : String, parameterValue : Integer)
{
	name := parameterName;
	type := "Integer";
	value := new EValue(parameterValue);
}

constructor trace::VarParameterValue::VarParameterValue(parameterName : String, typeName : String, parameterValue : ecore::EObject)
{
	name := parameterName;
	type := typeName;
	value := new EValue(parameterValue);
}

constructor trace::VarParameterValue::VarParameterValue(parameterName : String, typeName : String, objects : OrderedSet(OclAny))
{
	name := parameterName;
	type := "OrderedSet(" + typeName + ")";
	value := new EValue(objects);
}

constructor trace::EMappingResults::EMappingResults(typeName : String, resultObject : ecore::EObject)
{
	result.result := object trace::VarParameterValue {
		kind := trace::EDirectionKind::INOUT;
		name := "result";
		type := typeName;
		value := new EValue(resultObject);
	}
}






query String::getMappingModuleName() : String
{
	var transformationUtilsMappings := 
		"locationConstraint".oclAsSet()
		->including("titleStyle")
		->including("sortingStyle")
		->including("filteringStyle")
		->including("notationBounds")
		->including("fontStyle")
		->including("connectorToBendpoint");
	return if transformationUtilsMappings->includes(self) then "TransformationUtils" else TARGET_TRANSFORMATION_NAME endif;
}